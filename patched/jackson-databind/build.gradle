apply plugin: 'com.palantir.jigsaw-patch'

needsPatching {
    module = "com.fasterxml.jackson.databind"
}

dependencies {
    needsPatching 'com.fasterxml.jackson.core:jackson-databind'
}

File jarPath = configurations.needsPatching.getResolvedConfiguration().getFiles()[0]
String jarName = jarPath.getName().replace(".jar", "")

task jdeps(type: Exec) {
    // TODO(dfox): make these dependencies expressable nicely
    dependsOn tasks.getByPath(":patched:jackson-core:patchJar")
    dependsOn tasks.getByPath(":patched:jackson-annotations:patchJar")

    def outputDir = "$buildDir/jdeps/${jarName}"
    commandLine "jdeps", "-verbose",
        "--module-path", "../jackson-core/build/patchJar/jackson-core-2.9.5.jar:../jackson-annotations/build/patchJar/jackson-annotations-2.9.5.jar",
        "--generate-module-info", outputDir,
        jarPath
    outputs.dir file(outputDir)
}

task merge(type: Copy, dependsOn: [unpack, jdeps]) {
    from(jdeps.outputs.files)
    into("$buildDir/unpack/${jarName}")
    eachFile { details -> details.path = file(details.path).getName() }
    includeEmptyDirs = false
}

task compileModuleInfo(type: Exec, dependsOn: merge) {

    def input = file("$buildDir/unpack/${jarName}/module-info.java")
    def outputDir = file("$buildDir/compileModuleInfo/${jarName}")

    commandLine "javac",
        "-d", outputDir,
        "--module-path", "../jackson-core/build/patchJar/jackson-core-2.9.5.jar:../jackson-annotations/build/patchJar/jackson-annotations-2.9.5.jar",
        "--add-modules", needsPatching.module,
        "--patch-module", "${needsPatching.module}=$buildDir/unpack/${jarName}",
        input

    outputs.dir outputDir
}

task copyJarForPatching(type: Copy) {
    from jarPath
    into("$buildDir/patchJar")
}

task patchJar(type: Exec) {
    dependsOn copyJarForPatching
    dependsOn compileModuleInfo

    def jarToPatch = "$buildDir/patchJar/${jarName}.jar"
    def classFile = file("$buildDir/compileModuleInfo/${jarName}/module-info.class")
    def changeDir = "$buildDir/compileModuleInfo/${jarName}"

    commandLine "jar", "uf", jarToPatch, "-C", changeDir, classFile.getName()

    outputs.file "$buildDir/patchJar/${jarName}.jar"
}
