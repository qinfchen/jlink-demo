plugins {
    // https://github.com/gradle/gradle-java-modules/blob/master/src/main/java/org/gradle/java/JigsawPlugin.java
    id 'org.gradle.java.experimental-jigsaw' version '0.1.1'
    id 'application'
}

javaModule.name = 'cli'
mainClassName = 'cli.Main'

dependencies {
    implementation 'org.slf4j:slf4j-api'
    implementation 'org.slf4j:slf4j-simple' // should this be runtime ??
    implementation files('../patched/jackson-core/build/patchJar/jackson-core-2.9.5.jar') {
        builtBy tasks.getByPath(':patched:jackson-core:patchJar')
    }

    implementation files('../patched/jackson-annotations/build/patchJar/jackson-annotations-2.9.5.jar') {
        builtBy tasks.getByPath(':patched:jackson-annotations:patchJar')
    }

    implementation files('../patched/jackson-databind/build/patchJar/jackson-databind-2.9.5.jar') {
        builtBy tasks.getByPath(':patched:jackson-databind:patchJar')
    }

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

def java_home = System.getenv('JAVA_HOME')
task link(type: Exec) {
    dependsOn 'clean'
    dependsOn 'jar'

    workingDir 'build'

    def outputDir = "$buildDir/mydist";
    def modulePath = sourceSets.main.output.getAsPath() + ":" + configurations.compileClasspath.getAsPath()

    doFirst {
        "rm -rf ${outputDir}".execute()
    }

    commandLine "${java_home}/bin/jlink",
            '--module-path', modulePath,
            '--add-modules', 'cli',
            '--launcher', "cli=cli/${mainClassName}",
            '--output', outputDir,
            '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages'

    outputs.dir outputDir
}

task tgz(type: Tar, dependsOn: link) {
    compression = Compression.GZIP
    from(link.outputs.files)
    doLast {
        println "Distribution available at ${outputs.files.singleFile} (${outputs.files.singleFile.length() / 1000000} MB)"
    }
}

task doSomething(type: Exec, dependsOn: link) {
    workingDir "$buildDir/mydist"

    executable './bin/cli'
}

check.dependsOn doSomething
